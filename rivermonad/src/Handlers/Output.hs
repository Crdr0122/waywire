module Handlers.Output where

import Control.Concurrent.MVar
import Control.Monad (when)
import Control.Monad.State hiding (state)
import Data.Bimap qualified as B
import Data.List qualified as L
import Data.Map.Strict qualified as M
import Data.Maybe
import Foreign
import Foreign.C
import Optics.Core
import Optics.State
import Optics.State.Operators
import Types
import Utils.Helpers
import Wayland.ImportedFunctions

foreign export ccall "hs_output_position"
  hsOutputPosition :: Ptr () -> Ptr RiverOutput -> CInt -> CInt -> IO ()
foreign export ccall "hs_output_dimensions"
  hsOutputDimensions :: Ptr () -> Ptr RiverOutput -> CInt -> CInt -> IO ()
foreign export ccall "hs_output_removed"
  hsOutputRemoved :: Ptr () -> Ptr RiverOutput -> IO ()
foreign export ccall "hs_output_wl_output"
  hsOutputWlOutput :: Ptr () -> Ptr RiverOutput -> CUInt -> IO ()
foreign export ccall "hs_output_capture_sessions"
  hsOutputCaptureSessions :: Ptr () -> Ptr RiverOutput -> CUInt -> IO ()

hsOutputDimensions :: Ptr () -> Ptr RiverOutput -> CInt -> CInt -> IO ()
hsOutputDimensions dataPtr output width height = do
  stateMVar <- deRefStablePtr (castPtrToStablePtr dataPtr)
  modifyMVar_ (stateMVar :: MVar WMState) $ pure . (#allOutputs % at output %? #outGeometry %~ \g -> g & #rw .~ width & #rh .~ height)

hsOutputPosition :: Ptr () -> Ptr RiverOutput -> CInt -> CInt -> IO ()
hsOutputPosition dataPtr output x y = do
  stateMVar <- deRefStablePtr (castPtrToStablePtr dataPtr)
  modifyMVar_ (stateMVar :: MVar WMState) $ pure . (#allOutputs % at output %? #outGeometry %~ \g -> g & #rx .~ x & #ry .~ y)

hsOutputWlOutput :: Ptr () -> Ptr RiverOutput -> CUInt -> IO ()
hsOutputWlOutput dataPtr output wlOutput = do
  stateMVar <- deRefStablePtr (castPtrToStablePtr dataPtr)
  modifyMVar_ (stateMVar :: MVar WMState) $ pure . execState transform
 where -- This is only for restarting wm in same session
  transform = do
    #allOutputs % at output %? #outWlOutput .= wlOutput
    oWs <- use #allOutputWorkspaces
    use (#persistedStateOutputs % at (cuintToWord32 wlOutput)) >>= \case
      Just oldW | B.notMemberR oldW oWs -> do
        #allOutputWorkspaces %= B.insert output oldW
        #persistedStateOutputs % at (cuintToWord32 wlOutput) .= Nothing
      _ -> do
        let remainingWorkspace = fromMaybe 0 $ L.find (\n -> B.notMemberR n $ oWs) [1 ..]
        #allOutputWorkspaces %= B.insert output remainingWorkspace

    fO <- use #focusedOutput
    when (fO == nullPtr) $ #focusedOutput .= output

hsOutputRemoved :: Ptr () -> Ptr RiverOutput -> IO ()
hsOutputRemoved dataPtr removedOutput = do
  stateMVar <- deRefStablePtr (castPtrToStablePtr dataPtr)
  modifyMVar_ (stateMVar :: MVar WMState) $ execStateT transform
 where -- Add remember workspace
  transform = do
    liftIO $ riverOutputDestroy removedOutput

    use (#allOutputs % at removedOutput) >>= \case
      Nothing -> pure ()
      Just o -> do
        #allLayerShellOutputs %= M.delete (o ^. #outLayerShell)
        liftIO $ riverLayerShellOutputDestroy (o ^. #outLayerShell)
        #allOutputs %= M.delete removedOutput

    #allOutputWorkspaces %= B.delete removedOutput
    -- Delete first then check remaining
    use (pairOfGetter #focusedOutput (#allOutputWorkspaces % to B.keys)) >>= \case
      (currentFocusedOutput, []) | currentFocusedOutput == removedOutput -> #focusedOutput .= nullPtr
      (currentFocusedOutput, h : _) | currentFocusedOutput == removedOutput -> #focusedOutput .= h
      _ -> pure ()

hsOutputCaptureSessions :: Ptr () -> Ptr RiverOutput -> CUInt -> IO ()
hsOutputCaptureSessions _ _ _ = pure ()
